apply plugin: 'groovy'
apply plugin: 'application'

mainClassName = "org.folio.inventory.Launcher"
version = "3.1.0-SNAPSHOT"

ext.sharedManifest = manifest {
  attributes(
    "Main-Class": mainClassName,
    "Implementation-Title": "inventory",
    "Implementation-Version": version)
}

repositories {
  mavenCentral()
}

dependencies {
  compile project(':common')
  compile 'io.vertx:vertx-core:3.3.2'
  compile "io.vertx:vertx-lang-groovy:3.3.2"
  compile 'org.codehaus.groovy:groovy-all:2.4.8'
  compile 'io.vertx:vertx-web:3.3.2'
  compile 'com.google.guava:guava:19.0'
  compile 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'
  compile 'org.apache.commons:commons-lang3:3.5'

  testCompile 'junit:junit:4.12'
  testCompile 'org.hamcrest:hamcrest-junit:2.0.0.0'
  testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
  testCompile 'org.apache.commons:commons-io:1.3.2'
  testCompile 'org.apache.httpcomponents:httpclient-osgi:4.5.2'
  testCompile 'com.github.jsonld-java:jsonld-java:0.9.0'
}

test {
  //Gradle runs tests designed to be run in a suite twice
  //See: https://discuss.gradle.org/t/how-to-prevent-junit-test-suite-classes-from-being-ran-twice/7107/4
  exclude '**/External**Collection**Examples.class'
  exclude '**/api/**Examples.class'
}

task testApiDirect(type: Test) {
  include '**/api/ApiTestSuite.class'
}

task testApiViaOkapi(type: Test) {
  include '**/api/ApiTestSuite.class'

  systemProperty 'okapi.use', 'true'

  def okapiAddress = System.getProperty('okapi.address') ?: 'http://localhost:9130'

  systemProperty 'okapi.address', okapiAddress
}

task testExternalStorage(type: Test) {
  exclude '**/External**CollectionExamples.class'
  include '**/storage/external/ExternalStorageSuite.class'
  include '**/api/ApiTestSuite.class'

  systemProperty 'inventory.storage.use', 'external'

  println "Inventory Storage Config: ${System.getProperty('inventory.storage.address')}"

  systemProperty 'inventory.storage.address', System.getProperty('inventory.storage.address')
}

jar {
  manifest = project.manifest {
    from sharedManifest
  }
}

task fatJar(type: Jar) {
  manifest = project.manifest {
    from sharedManifest
  }
  archiveName 'inventory.jar'
  from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
  with jar
}

task wrapper(type: Wrapper) {
  gradleVersion = '3.3'
}
